#!ipxe
###
### netboot.xyz-custom menu
###


### Set a background picture * Not enabled in netboot.xyz build
### console --picture http://boot.ipxe.org/ipxe.png

### Set a background picture with margins * Not enabled in netboot.xyz build
### console --picture http://boot.ipxe.org/texture.png --left 32 --right 32 --top 32 --bottom 48

### Check CPU architecture and if BIOS or UEFI is used
iseq ${buildarch} i386 && set cpuarch i386 ||
iseq ${buildarch} i386 && set cpuarch_text 32bit ||
iseq ${buildarch} x86_64 && set cpuarch x86_64 ||
iseq ${buildarch} x86_64 && set cpuarch_text 64bit ||
iseq ${buildarch} arm64 && set cpuarch arm64 ||
iseq ${buildarch} arm64 && set cpuarch_text 64bit ARM ||
iseq ${platform} pcbios && set fwplatform BIOS ||
iseq ${platform} efi && set fwplatform UEFI ||

### Permanently require trusted images
#imgtrust --permanent

### Indent
set spaces2:hex 20:20
set spaces4:hex 20:20:20:20
set spaces8:hex 20:20:20:20:20:20:20:20
set spaces2 ${spaces2:string}
set spaces4 ${spaces4:string}
set spaces8 ${spaces8:string}
  
:dasboot-main-menu
#clear dasboot-main-menu_choice
menu *** DasBoot 2018-03-18 ***
item --gap ${manufacturer} ${product} in ${fwplatform} Mode w/ ${cpuarch_text} CPU
#and ${memsize} MB RAM * Not enabled in netboot.xyz build
#tem --gap ${NIC} ${MAC}
item --key p systeminfo-lspci        BIOS and UEFI: List all (P)CI devices with lspci
item --key h systeminfo-hdt          BIOS-only${spaces4}: (H)DT
item
item
item --gap NetBoot:
item --key d diagnostic-submenu      ${spaces2}(D)iagnostic Tools
item --key b bios-submenu            ${spaces2}(B)IOS Updates
item --key f firmware-submenu        ${spaces2}(F)irmware Updates
item --key w wiping-submenu          ${spaces2}(W)iping/Media Sanitation Tools
item
item
item --gap Other:
item --key c exit                    ${spaces2}(C)hangelog
item --key x exit                    ${spaces2}e(X)it and go back to netboot menu
item --key s shell                   ${spaces2}drop to iPXE (S)hell
item --key r reboot                  ${spaces2}(R)eboot

choose dasboot-main-menu_choice && goto ${dasboot-main-menu_choice} || goto error
#echo ${cls}
#goto ${dasboot-main-menu_choice}
#goto exit

:systeminfo-lspci
### copied from http://boot.salstar.sk/lspci.ipxe and slightly modified for it to work w/ netboot.xyz-custom/dasboot
### see also http://ipxe.org/cmd/pciscan
clear addr
pciscan addr && goto systeminfo-lspci_pciscan_found ||
echo Missing "pciscan" command. Recompile ipxe with CMD_PCI.
prompt
goto dasboot-main-menu
:systeminfo-lspci_pciscan_found
### HTTPS fails; not sure why
imgfetch http://boot.salstar.sk/pciids.ipxe || goto failed_download
### verify fails; not sure why
### imgverify pciids.ipxe http://boot.salstar.sk/sigs/pciids.ipxe.sig || goto failed_verify
clear addr
menu boot.salstar.sk's lspci with updated PCI.IDs (2018-03-15)
item --gap PCI Device List:
:systeminfo-lspci_pciscan
pciscan addr || goto systeminfo-lspci_pciscan_done
clear ven
clear dev
set vendor ${pci/${addr}.0.2}
set device ${pci/${addr}.2.2}
chain pciids.ipxe
item --gap ${addr:busdevfn} ${spaces4} ${ven}
item b${addr:busdevfn} ${spaces2} ${vendor}:${device} ${dev}
goto systeminfo-lspci_pciscan
:systeminfo-lspci_pciscan_done
choose press_enter ||
imgfree pciids.ipxe
goto dasboot-main-menu

:systeminfo-hdt
kernel hdt.c32
module hdt/modules.alias || goto error
module hdt/pci.ids || goto error
boot || goto error
goto dasboot-main-menu

:bios-submenu
echo https://raw.githubusercontent.com/${github_user}/netboot.xyz-custom/master/${dasboot-main-menu_choice}.ipxe || goto error
prompt
goto dasboot-main-menu

:diagnostic-submenu
echo https://raw.githubusercontent.com/${github_user}/netboot.xyz-custom/master/${dasboot-main-menu_choice}.ipxe || goto error
prompt
goto dasboot-main-menu

:firmware-submenu
echo chain firmware-submenu.ipxe || goto error
prompt
goto dasboot-main-menu

:wiping-submenu
echo chain wiping-submenu.ipxe || goto error
prompt
goto dasboot-main-menu

:changelog
echo chain changelog.ipxe
prompt
goto dasboot-main-menu

:exit
#if chaining here, e.g. netboot's main menu.ipxe, the *.ipxe script has to be in the same location as this script
exit

:shell
echo Type 'exit' to return to the menu
shell
goto dasboot-main-menu

:reboot
reboot

:failed_download
echo
echo Failed to download a file.
goto error

:failed_verify
echo
echo Failed to verify a file.
goto error

:error
echo Error occured, press any key to return to main menu ...
prompt
goto dasboot-main-menu
